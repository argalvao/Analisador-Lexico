! -----------------------------------------------------------------------
!Gram√°tica desenvolvida como parte do problema 2 do MI PROCESSADORES DE
!LINGUAGEM DE PROGRAMA√á√ÉO. Essa gram√°tica auxiliar√° no desenvolvimento dos
!demais componentes que constam um compilador. Ser√° de extrema import√¢ncia
!no processo de gera√ß√£o da √°rvore sint√°tica que, como o pr√≥prio nome sugere,
!ser√° utilizado pelo analisador sint√°tico.
! Foi tirado das produÁıes dos seguintes de <Corpo> o "}"
! -----------------------------------------------------------------------

"Name" = 'PBL Compiladores'

"Author" = 'Abel Galv√£o, Caique Trindade, Emerson Souza, Felipe Damascena, Pedro Brand√£o, Ramon Silva, Solenir Figuer√™do, Victor Souza'

"Version" = '1.0'

"About" = 'Grammar'

"Start Symbol" = <Inicio>

"Case Sensitive" = True

!Defini√ß√£o de express√£o regulares e conjuntos a serem utilizados na Gram√°tica

Id = {Letter}({AlphaNumeric} | '_')*
Real = ('-')? {Digit} + '.' {Digit} +
IntPos = {Digit}+
IntNeg = '-'{Digit}+
Int = IntPos + IntNeg
{String Ch} = {Printable} - ["] + [\"]
String = ["] {String Ch}* ["]
Numero = Int + Real
Boolean = 'true' + 'false'

<Valor> ::= Numero | String | Boolean | Id

<ValorVetor> ::= IntPos | Id

<Tipo> ::= 'int' | 'boolean' | 'string' | 'real' | Id
	
<Inicio> ::= <Const> <Struct> <Var> <GeraFuncaoeProcedure> <Start>

!Bloco Condicional

<Condicional> ::= 'if' '(' <ExpressaoLogicaRelacional> ')' 'then' '{'<Corpo> <CondEnd>

<CondEnd> ::= 'else' '{' <Corpo> |

!Bloco de repeti√ß√£o

<Laco> ::= 'while' '(' <ExpressaoLogicaRelacional> ')' '{'<Corpo> 

! Cria√ß√£o fun√ß√£o e procedimento

<GeraFuncaoeProcedure> ::= <Funcao> <GeraFuncaoeProcedure> | <Procedimento> <GeraFuncaoeProcedure> |

! Bloco de fun√ß√£o e procedimento

<Funcao> ::= 'function' <Tipo> Id '(' <Parametro>

<Procedimento> ::= 'procedure' Id '(' <Parametro>

<Parametro> ::= <Tipo> Id <Para2> <Para1>

<Para1> ::= ',' <Parametro> | ')' <F2>

<Para2> ::= '[' ']' <Para3> |

<Para3> ::= '[' ']' |

<F2> ::= '{' <Corpo>

!Bloco de declara√ß√£o de constantes

<Const> ::= 'const' '{' <TipoConst> |

<TipoConst> ::= <Tipo> <IdConst>

<IdConst> ::= Id <Valor> <Const2>

<Const2> ::= ',' <IdConst> | ';' <Const3>

<Const3> ::= '}' | <TipoConst>

!Bloco de declara√ß√£o de structs

<Struct> ::= 'typedef' 'struct' Id <Extends> <TipoStruct> <Struct> |

<Extends> ::= 'extends' Id '{' | '{'

<TipoStruct> ::= <Tipo> <IdStruct>

<IdStruct> ::= Id <Struct2>

<Struct2> ::= ',' <IdStruct> | ';' <Struct3>

<Struct3> ::= '}' | <TipoStruct>

!Bloco de declara√ß√£o de vari√°veis

<Var> ::= 'var' '{' <TipoVar>  |

<TipoVar> ::= <Tipo> <IdVar>

<IdVar> ::= Id <Var2>

<Var2> ::= ',' <IdVar> | ';' <Var3> | '=' <Valor> <Var4> | <VetorDeclaracao> <Var4>

<Var3> ::= '}' | <TipoVar>

<Var4> ::= ',' <IdVar> | ';' <Var3>

<VetorDeclaracao> ::= '[' <ValorVetor> ']' <Matriz>

<Matriz> ::= '[' <ValorVetor> ']' <Var4> | <Var4>

!Chamada de uma procedimento / fun√ß√£o

<ListaParametros> ::= <ListaParametros2> <ContListaParametros>

<ContListaParametros> ::= ',' <ListaParametros> |

<ListaParametros2> ::= <Identificador> | Numero | String

!Acesso a Identificadores (vari√°veis, structs, matrizes e fun√ß√µes)

<Identificador> ::= <Escopo> Id <Identificador2> | Id <Identificador3>

<Identificador2> ::= '.' Id <Vetor> | <Vetor> |

<Identificador3> ::= <Identificador2> | '(' <ListaParametros> ')'

<Identificador4> ::= '.' Id <Vetor> |

<Vetor> ::= '[' <IndiceVetor> ']' <Vetor2> <Identificador4> | <Identificador4>

<Vetor2> ::= '[' <IndiceVetor> ']' |

<IndiceVetor> ::= IntPos | <Identificador>

<Escopo> ::= 'local' '.' | 'global' '.'

!Acesso a Identificadores (sem fun√ß√µes)

<IdentificadorSemFuncao> ::= <Escopo> Id <Identificador2> | Id <Identificador2>

!Express√µes Aritm√©ticas

<ExpressaoAritmetica> ::= <T> <E2> | <IdentificadorAritmetico> | '++' <IdentificadorSemFuncao> <T2> <E2> | '--' <IdentificadorSemFuncao> <T2> <E2>

<ExpressaoAritmetica2> ::= '++' <T2> <E2> | '--' <T2> <E2> | <T2> <E2>

<E2> ::= '+' <ExpressaoAritmetica> | '-' <ExpressaoAritmetica> |

<T> ::= <F> <T2>

<T2> ::= '*' <ExpressaoAritmetica> | '/' <ExpressaoAritmetica> |

<F> ::= '(' <ExpressaoAritmetica> ')' | Numero

<IdentificadorAritmetico> ::= <Escopo> Id <Identificador2> <ExpressaoAritmetica2> | Id <IdentificadorAritmetico3>

<IdentificadorAritmetico3> ::= <Identificador2> <ExpressaoAritmetica2> | '(' <ListaParametros> ')' <T2> <E2>

!Express√µes Relacionais e L√≥gicas

<ExpressaoLogicaRelacional> ::= <ExpressaoLR> | '(' <ExpressaoLR> ')' <ExpressaoLR3>

<ExpressaoLR> ::= <ArgumentoLR2> <ExpressaoLR2> | <ArgumentoLR3> <OperadorRelacional> <ArgumentoLR> <ExpressaoLR3>

<ExpressaoLR2> ::= <OperadorRelacional> <ArgumentoLR> <ExpressaoLR3> | <ExpressaoLR3>

<ExpressaoLR3> ::= <OperadorLogico> <ExpressaoLogicaRelacional> |

<ArgumentoLR> ::= <ArgumentoLR2> | <ArgumentoLR3>

<ArgumentoLR2> ::= '!' <ArgumentoLR2_1> | 'true' | 'false'

<ArgumentoLR2_1> ::= Id | 'true' | 'false'

<ArgumentoLR3> ::= String | <ExpressaoAritmetica>

<OperadorRelacional> ::= '!=' | '==' | '<' | '>' | '<=' | '>='

<OperadorLogico> ::= '&&' | '||'

!Fun√ß√£o print

<Print> ::= 'print' '(' <Print1>

<Print1> ::= String <AuxPrint> | <Identificador> <AuxPrint> | Numero <AuxPrint>

<AuxPrint> ::= ',' <Print1> | <PrintFim>

<PrintFim> ::= ')' ';'

!Fun√ß√£o read

<Read> ::= 'read' '(' <Read1>

<Read1> ::= <IdentificadorSemFuncao> <AuxRead>

<AuxRead> ::= ',' <Read1> | <ReadFim>

<ReadFim> ::= ')' ';'

!Corpo

<Corpo> ::= <Var> <Corpo2> '}' | '}'

<Corpo2> ::= <Comandos> <Corpo2> |

<Comandos> ::= <Condicional> | <Laco> | <Read> | <Print> | <ComandosReturn> | <IdentificadorComandos>

<IdentificadorComandos> ::= <IdentificadorSemFuncao> <IdentificadorComandos2> ';'

<IdentificadorComandos2> ::= '=' <IdentificadorComandos2_1> | '(' <ListaParametros> ')'

<IdentificadorComandos2_1> ::= <ExpressaoAritmetica> | String | Boolean

<ComandosReturn> ::= 'return' <CodigosRetornos>

<CodigosRetornos> ::= ';' | <ExpressaoAritmetica> ';'

!Chamada do Start

<Start> ::= 'start' '(' ')' '{' <Corpo>

